buildscript {
    ext {
        kotlin_version = '1.6.21'
        okhttp_version = '4.9.3'
        retrofit_version = '2.9.0'
        room_version = "2.4.2"
        rxjava_version = "2.2.13"
        rxandroid_version = "2.1.1"
        rxbinding_version = "3.0.0-alpha2"
        mvrx_version = "1.5.1"
        dagger_version = "2.41"
        assistedinject_version = "0.5.2"
        stetho_version = "1.6.0"
        viewpager_version = "1.0.0"
        core_ktx_version = "1.8.0-beta01"
        appcompat_version = "1.5.0"
        material_version = "1.7.0-beta01"
        recycler_version = "1.2.1"
        timber_version = "5.0.1"
        junit_version = "4.12"
        espresso_runner_version = "1.4.0"
        espresso_core_version = "3.4.0"
        picasso_version = "2.8"
        constraint_version = "2.1.4"
        threeten_version = "1.4.0"
        photoview_version = "2.3.0"
        firebase_version = "20.1.2"
        crashlytics_version = "18.2.9"
        firebase_perf_version = "20.0.6"
        simplestore_version = "0.0.7"
        leakcanary_version = "2.9.1"
        lifecycle_version = "2.4.1"
    }

    repositories {
        google()

        // Gradle license plugin snapshot
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    }

    dependencies {
        // Android, lol
        classpath 'com.android.tools.build:gradle:7.2.2'

        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // Firebase
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:perf-plugin:1.4.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'

        // License downloader
        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.91'

        // Gradle Play Publisher
        classpath 'com.github.triplet.gradle:play-publisher:3.7.0'
    }
}

plugins {
    // Detekt
    id "io.gitlab.arturbosch.detekt" version "1.20.0"
}

allprojects {
    // Detekt
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        config = files("$rootDir/default-detekt-config.yml")
    }

    repositories {
        mavenCentral()
        google()

        // For PhotoView
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'kotlin-android'

    android {
        compileSdkVersion 32

        defaultConfig {
            minSdkVersion 21
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }

        kotlinOptions {
            jvmTarget = 1.8
        }
    }

    // Treat all Kotlin warnings as errors
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
}
