// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.3.50'
        okhttp_version = '3.14.1'
        retrofit_version = '2.5.0'
        room_version = "2.1.0-beta01"
        lifecycle_version = "2.0.0"
        paging_version = "2.1.0"
        rxjava_version = "2.2.6"
        mvrx_version = "1.0.1"
        dagger_version = "2.16"
        assistedinject_version = "0.4.0"
        stetho_version = "1.5.1"
        viewpager_version = "1.0.0-beta04"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.23.0"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC15"
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        config = files("$rootDir/default-detekt-config.yml")
        filters = ".*build.*,.*/resources/.*,.*/tmp/.*"
    }

    repositories {
        google()
        jcenter()
    }
}

subprojects {
    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'com.diffplug.gradle.spotless'

    android {
        compileSdkVersion 29
        defaultConfig {
            minSdkVersion 21
        }
    }

    spotless {
        kotlin {
            target "**/*.kt"
            ktlint()
        }
    }

    // Treat all Kotlin warnings as errors
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
}
